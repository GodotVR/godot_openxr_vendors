#!/usr/bin/env python
from glob import glob
from pathlib import Path

Import("env", "common_objects")

env_pico = env.Clone()

env_pico.Append(CPPPATH=[
    "src/main/cpp",
    ])

# Source files
sources = common_objects.copy()
sources += Glob("src/main/cpp/*.cpp")
sources += Glob("src/main/cpp/export/*.cpp")

binary_path = '#demo/addons/godotopenxrvendors/pico/.bin'
project_name = 'godotopenxrpico'

# Create the library target
if env_pico["platform"] == "android":
    print("Use gradle to generate the Android binaries")
    Exit(255)
elif env_pico["platform"] == "macos":
    library = env_pico.SharedLibrary(
        "{0}/lib{1}.{2}.{3}.framework/{1}.{2}.{3}".format(
            binary_path,
            project_name,
            env_pico["platform"],
            env_pico["target"],
        ),
        source=sources,
    )
else:
    library = env_pico.SharedLibrary(
        "{}/lib{}.{}.{}.{}{}".format(
            binary_path,
            project_name,
            env_pico["platform"],
            env_pico["target"],
            env_pico["arch"],
            env_pico["SHLIBSUFFIX"],
        ),
        source=sources,
    )

Default(library)
