#!/usr/bin/env python
from glob import glob
from pathlib import Path

Import("env", "common_objects")

env_meta = env.Clone()

env_meta.Append(CPPPATH=[
    "src/main/cpp",
    "src/main/cpp/include",
    "libs/ovr_openxr_mobile_sdk/OpenXR/Include",
    ])

# Source files
sources = common_objects.copy()
sources += Glob("src/main/cpp/*.cpp")
sources += Glob("src/main/cpp/export/*.cpp")

binary_path = '#demo/addons/godotopenxrvendors/meta/.bin'
project_name = 'godotopenxrmeta'

# Create the library target
if env_meta["platform"] == "android":
    print("Use gradle to generate the Android binaries")
    Exit(255)
elif env_meta["platform"] == "macos":
    library = env_meta.SharedLibrary(
        "{0}/lib{1}.{2}.{3}.framework/{1}.{2}.{3}".format(
            binary_path,
            project_name,
            env_meta["platform"],
            env_meta["target"],
        ),
        source=sources,
    )
else:
    library = env_meta.SharedLibrary(
        "{}/lib{}.{}.{}.{}{}".format(
            binary_path,
            project_name,
            env_meta["platform"],
            env_meta["target"],
            env_meta["arch"],
            env_meta["SHLIBSUFFIX"],
        ),
        source=sources,
    )

Default(library)
