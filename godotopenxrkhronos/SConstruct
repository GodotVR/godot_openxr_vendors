#!/usr/bin/env python
from glob import glob
from pathlib import Path

Import("env", "common_objects")

env_khronos = env.Clone()

env_khronos.Append(CPPPATH=[
    "src/main/cpp",
    ])

# Source files
sources = common_objects.copy()
sources += Glob("src/main/cpp/*.cpp")
sources += Glob("src/main/cpp/export/*.cpp")

binary_path = '#demo/addons/godotopenxrvendors/khronos/.bin'
project_name = 'godotopenxrkhronos'

# Create the library target
if env_khronos["platform"] == "android":
    print("Use gradle to generate the Android binaries")
    Exit(255)
elif env_khronos["platform"] == "macos":
    library = env_khronos.SharedLibrary(
        "{0}/lib{1}.{2}.{3}.framework/{1}.{2}.{3}".format(
            binary_path,
            project_name,
            env_khronos["platform"],
            env_khronos["target"],
        ),
        source=sources,
    )
else:
    library = env_khronos.SharedLibrary(
        "{}/lib{}.{}.{}.{}{}".format(
            binary_path,
            project_name,
            env_khronos["platform"],
            env_khronos["target"],
            env_khronos["arch"],
            env_khronos["SHLIBSUFFIX"],
        ),
        source=sources,
    )

Default(library)
